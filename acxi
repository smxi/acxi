#!/usr/bin/perl
#########################################################################
# acxi - audio conversion program
#########################################################################

# Copyright (c) 2010-2018 - Harald Hope - smxi.org 
# Home page: https://github.com/smxi/acxi
# Forum support: https://techpatterns.com/forums/about1491.html
# Download url: https://smxi.org/acxi
#
# Based on flac2ogg.pl
# Copyright (c) 2004 - Jason L. Buberel - jason@buberel.org
# Copyright (c) 2007 - Evan Boggs - etboggs@indiana.edu
# Previous Home page (gone now): 
#   http://www.buberel.org/linux/batch-flac-to-ogg-converter.php
#
# Modified: 2018-12-05 - Cleaned up code, refactored
# Modified: 2011-07-26 - Harald Hope - Added patch for $ in file names; 
#   changed verbosity levels to fit future 3 release, got rid of 
#   $B_SILENT and $B_QUIET
# Modified: 2011-03-23 - Odd Eivind Ebbesen - www.oddware.net - 
#   <oddebb at gmail dot com>
#   Added functionality for Flac conversion to MP3, preserving tags.
#########################################################################
# This program is free software; you can redistribute it and/or modify 
# it under the terms of the GNU General Public License as published by 
# the Free Software Foundation; either version 3 of the License, or 
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful, but 
# WITHOUT ANY WARRANTY; without even the implied warranty of 
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU 
# General Public License for more details.
#
# Get the full text of the GPL here: http://www.gnu.org/licenses/gpl.txt
#########################################################################
# Given a source directory tree of original lossless music files 
# (flac, wav, etc), this program will recreate (or add to) a new 
# directory tree of ogg/mp3 files by recursively encoding only new 
# source files to destination types.
# The source and destination directories can be hard-coded using the 
# $SOURCE_DIRECTORY and $DESTINATION_DIRECTORY variables, or passed on 
# the command line, or can be set in the configuration file (recommended).  
#
# See BEGIN USER MODIFIABLE VALUES for config file information.
#########################################################################
### NO USER CHANGES IN THIS SECTION ###
#########################################################################
use strict;
use warnings;
# use diagnostics;
use 5.010;
use Getopt::Long qw(GetOptions);
Getopt::Long::Configure ('bundling', 'no_ignore_case', 
'no_getopt_compat', 'no_auto_abbrev','pass_through');
use File::stat;
use File::Find;
use File::Copy qw(copy);
# only use for debugging, some distros do not ship with core modules.
#use Data::Dumper qw(Dumper);

#########################################################################
### PROGRAM GLOBALS - DO NOT TOUCH THESE! ###
#########################################################################

## SELF INFO
my $SELF_NAME = 'acxi';
my $SELF_VERSION = '3.0.0';
my $SELF_DATE = '2018-12-07';

## GLOBALS
my ($B_DEST_CHANGED,$B_FORCE,$B_QUIET,$B_TEST);
my ($PRINT_LINE_HEAVY,$PRINT_LINE_LARGE,$PRINT_LINE_SMALL);
my ($SILENT_ARG_FO,$SILENT_ARG_LAME) = ('','');
my ($EXTENSION,@EXTENSION_LIST,@FOUND_LIST,$LIST_TYPE);

## CONSTANTS
my $LINE_HEAVY = "===========================================================================\n";
my $LINE_SMALL = "-----------------------------------------------------------------\n";
my $LINE_LARGE = "---------------------------------------------------------------------------\n";

#########################################################################
### BEGIN USER MODIFIABLE VALUES ###
#########################################################################
# User config file at:
# Global: /etc/acxi.conf
# User override files checked in the following order (first found used):
# $XDG_CONFIG_HOME/acxi.conf, $HOME/.config/acxi.conf, $HOME/.acxi.conf
#
# Set values like this:
# Do not use the $ or @ preceding the variable name, or the semicolon 
# or single/double quote marks in the config file. Use this syntax for 
# config files:
# SOURCE_DIRECTORY=/home/fred/music/flac
# DESTINATION_DIRECTORY=/home/fred/music/ogg
#
# It's highly recommended to create a config file so you don't have to
# update the values below every time acxi updates.
#
# Anything in configs or in this section will be overridden if you use
# a startup argument.

## APPLICATION PATHS
my $COMMAND_OGG = '/usr/bin/oggenc';
my $COMMAND_FLAC = '/usr/bin/flac'; 
my $COMMAND_LAME = '/usr/bin/lame';
# metaflac is required ONLY for flac to mp3, to copy over the ID3 tags
# If you are not going to make MP3s, only OGG files for output, you do 
# not need this.
my $COMMAND_METAFLAC = '/usr/bin/metaflac';

## ASSIGN MUSIC SOURCE/DESTINATION DIRECTORY PATHS
##  $SOURCE_DIRECTORY is the original, working, like flac, wav, etc
##  $DESTINATION_DIRECTORY is the processed, ie, ogg, mp3
##  CHANGE TO FIT YOUR SYSTEM - do not end in /
## IMPORTANT: DESTINATION_DIRECTORY cannot be equal to SOURCE_DIRECTORY
my $SOURCE_DIRECTORY = '/path/to/source/directory';
my $DESTINATION_DIRECTORY = '/path/to/your/output/directory';

## INPUT/OUTPUT
my $QUALITY = 7;
# The following are NOT case sensitive,ie flac/FLAC, txt/TXT will be 
# found. INPUT_TYPE and OUTPUT_TYPE will be forced to lower case 
# internally.
my $INPUT_TYPE = 'flac';
my $OUTPUT_TYPE = 'ogg';
## NOTE: if you want to override $COPY_TYPES in your config files, you
# must use this syntax:
# COPY_TYPES=doc,docx,bmp,jpg,jpeg
# Add or remove types to copy over to ogg directories, do not include
# the input/output audio file types, only extra data types like txt.
# If you want no copying done, simply change to: $COPY_TYPES = 'none';
my $COPY_TYPES = 'bmp,doc,docx,gif,jpg,jpeg,odt,pdf,png,tif,txt';

# You can turn these to always on either here or in config file by setting to 
# desired verbosity level here directly, or in config file. 
# 0 = quiet/silent - no output at all; 
# 1 = default - single line per operation. This is the default, so you don't need 
#     to change it.
# 2 = verbose - but without the actual conversion data from codecs
# 3 = debug -  all available information.
my $LOG_LEVEL = 1;

#########################################################################
### END USER MODIFIABLE VALUES ###
#########################################################################

### MAIN - FUNCTIONS ###
sub error_handler {
	my ($error,$message) = @_;
	my ($br,$error_no,$error_text) = ("\n",0,'');
	if ($error eq 'unsupported-type'){$error_no = 2;}
	elsif ($error eq 'dest-dir'){$error_no = 1;}
	elsif ($error eq 'dest-eq-src-dir'){$error_no = 9;}
	elsif ($error eq 'missing-app'){$error_no = 3;}
	elsif ($error eq 'quality-invalid'){$error_no = 4;}
	elsif ($error eq 'bad-level'){$error_no = 5;}
	elsif ($error eq 'stat-infile'){$error_no = 6;}
	elsif ($error eq 'missing-arg'){$error_no = 7;$message = "Option: $message";$br=''}
	elsif ($error eq 'unsupported-option'){$error_no = 8;$br=''}
	print "${br}Error $error_no: $message\n";
	exit $error_no;
}

### SET DATA ###
sub set_basic_data {
	# if --copy/-c is set, then use that data instead of default copy types
	if ( defined $COPY_TYPES ){
		@EXTENSION_LIST = split( /,\s*/, $COPY_TYPES );
	}
	$DESTINATION_DIRECTORY =~ s|/$||;
	$SOURCE_DIRECTORY =~ s|/$||;
	$INPUT_TYPE = lc($INPUT_TYPE) if $INPUT_TYPE;
	$OUTPUT_TYPE = lc($OUTPUT_TYPE) if $OUTPUT_TYPE;
}
sub set_display_output_data {
	if ( $LOG_LEVEL < 3 ) {
		if ( $OUTPUT_TYPE eq 'mp3' ) {
			$SILENT_ARG_FO = '--silent'; # flac output 
			$SILENT_ARG_LAME = '--silent'; # lame output
		}
		elsif ( $OUTPUT_TYPE eq 'ogg' ) {
			$SILENT_ARG_FO = '--quiet'; # for oggenc output
		}
	}
	if ( $LOG_LEVEL < 2 ) {
		$PRINT_LINE_HEAVY = '';
		$PRINT_LINE_LARGE = '';
		$PRINT_LINE_SMALL = '';
	}
	else {
		$PRINT_LINE_HEAVY = 'print $LINE_HEAVY';
		$PRINT_LINE_LARGE = 'print $LINE_LARGE';
		$PRINT_LINE_SMALL = 'print $LINE_SMALL';
	}
}

### VALIDATIONS ###
sub validate_start_text {
	if ( $LOG_LEVEL > 0 ) {
		eval $PRINT_LINE_HEAVY;
		print "Running $SELF_NAME pretests.\n";
	}
}
sub validate_in_out_types {
	my ($b_type_unsupported, $error_message) = (0,'');
	if ( $LOG_LEVEL > 0 ) {
		eval $PRINT_LINE_LARGE;
		print dotify("Checking input and output types");
	}
	if ( $INPUT_TYPE !~ m/^(flac|wav|raw)$/i ){
		$b_type_unsupported = 1;
		$error_message .= "\n  The input type you entered is not supported: ";
		$error_message .= "$INPUT_TYPE";
	}
	if ( $OUTPUT_TYPE !~ m/^(ogg|mp3)$/i ){
		$b_type_unsupported = 1;
		$error_message .= "\n  The output type you entered is not supported: ";
		$error_message .= "$OUTPUT_TYPE";
		if ( $OUTPUT_TYPE eq 'mp3' && $INPUT_TYPE ne 'flac' ){
			$b_type_unsupported = 1;
			$error_message .= "\n  The output type $OUTPUT_TYPE you entered ";
			$error_message .= "currently only supports input type: flac";
		}
	}
	if ( $b_type_unsupported ){
		error_handler('unsupported-type',$error_message);
	}
	elsif ( $LOG_LEVEL > 0 ) {
		print "Valid: $INPUT_TYPE(in) $OUTPUT_TYPE(out)\n";
	}
}
sub validate_src_dest_directories {
	my ($b_missing_dir, $missing_dirs) = (0,'');
	if ( $LOG_LEVEL > 0 ) {
		eval $PRINT_LINE_LARGE;
		print dotify("Checking source / destination directories");
	}
	if ( ! -d "$SOURCE_DIRECTORY" ){
		$b_missing_dir = 1;
		$missing_dirs .= "\n  Source Directory: $SOURCE_DIRECTORY";
	}
	if ( ! -d "$DESTINATION_DIRECTORY" ){
		$b_missing_dir = 1;
		$missing_dirs .= "\n  Destination Directory: $DESTINATION_DIRECTORY";
	}
	if ( $b_missing_dir ) {
		my $error_message = "The paths for the following directories are missing:";
		$error_message .= "$missing_dirs";
		$error_message .= "\nUnable to continue. Please check the directory paths ";
		$error_message .= "you provided.";
		error_handler('dest-dir',$error_message);
	} 
	elsif ( $LOG_LEVEL > 0 ) {
		print "Directories: exist\n";
	}
	if (!$b_missing_dir && $DESTINATION_DIRECTORY eq $SOURCE_DIRECTORY){
		error_handler('dest-eq-src-dir',"Destination directory cannot be same as Source directory!");
	}
}
sub validate_application_paths {
	my ($b_missing_app, $error_message, $app_paths) = (0,'',''); 
	if ( $LOG_LEVEL > 0 ) {
		print dotify("Checking audio conversion tool path");
	}
	if ( $OUTPUT_TYPE eq 'ogg' ) {
		$app_paths .= "\n $COMMAND_OGG";
		if ( ! -x "$COMMAND_OGG" ) {
			$b_missing_app = 1;
			$error_message .= "\n  Encoding application not available: $COMMAND_OGG";
		}
	}
	elsif ( $OUTPUT_TYPE eq 'mp3' ) {
		$app_paths .= "\n $COMMAND_LAME ";
		if ( ! -x "$COMMAND_LAME" ) {
			$b_missing_app = 1;
			$error_message .= "\n  Encoding application not available: $COMMAND_LAME";
		}
		if ($INPUT_TYPE eq 'flac') {
			$app_paths .= "$COMMAND_FLAC ";
			if ( ! -x "$COMMAND_FLAC" ) {
				$b_missing_app = 1;
				$error_message .= "\n  Input processor $COMMAND_FLAC needed by lame ";
				$error_message .= "not available.";
			}
			$app_paths .= "$COMMAND_METAFLAC ";
			# Added: Odd @2011-03-23 01:55:28
			if ( ! -x "$COMMAND_METAFLAC" ) {
				$b_missing_app = 1;
				$error_message .= "\n  $COMMAND_METAFLAC not found. Required to copy ";
				$error_message .= "ID3 tags from Flac to MP3.";
			}
		}
		else {
			$b_missing_app = 1;
			$error_message .= "\n  $COMMAND_LAME currently only supports ";
			$error_message .= "flac as input.";
		}
	}
	if ( $b_missing_app ) {
		error_handler('missing-app',$error_message);
	}
	elsif ( $LOG_LEVEL > 0 ) {
		print "Available: $app_paths\n";
	}
}
sub validate_quality{
	my ($b_bad_quality, $error_message) = (0,'');
	if ( $LOG_LEVEL > 0 ) {
		print dotify("Checking quality support for $OUTPUT_TYPE");
	}
	if ( lc($OUTPUT_TYPE) eq 'ogg' && $QUALITY !~ m/^([1-9]|10)$/ ) {
		$b_bad_quality = 1;
		$error_message .= "\n  $OUTPUT_TYPE only supports ";
		$error_message .= "1-10 quality. You entered: $QUALITY";
	}
	elsif ( lc($OUTPUT_TYPE) eq 'mp3' && $QUALITY !~ m/^([0-9])$/ ) {
		$b_bad_quality = 1;
		$error_message .= "\n  $OUTPUT_TYPE only supports 0-9 quality. ";
		$error_message .= "You entered: $QUALITY";
	}
	if ( $b_bad_quality ) {
		error_handler('quality-invalid',$error_message);
	}
	elsif ( $LOG_LEVEL > 0 ) {
		print "Supported: $QUALITY\n";
	}
}
sub validate_log_level {
	my ($b_bad_level, $error_message) = (0,'');
	if ( $LOG_LEVEL > 0 ) {
		print dotify("Checking log/screen output level");
	}
	if ( $LOG_LEVEL !~ m/^([0-3])$/ ) {
		$b_bad_level = 1;
		$error_message .= "\n  LOG_LEVEL only supports 0-3. ";
		$error_message .= "You used: $LOG_LEVEL";
	}
	if ( $b_bad_level ) {
		error_handler('bad-level',$error_message);
	}
	elsif ( $LOG_LEVEL > 0 ) {
		print "Supported: $LOG_LEVEL\n";
	}
}

### SYNC MUSIC DIRECTORIES ###
sub sync_music_collections {
	my (@extension_files);
	# set the @EXTENSION_LIST if required by -c/--copy override
	set_basic_data();
	@EXTENSION_LIST = (@EXTENSION_LIST,$INPUT_TYPE);
	eval $PRINT_LINE_HEAVY;
	if ( $LOG_LEVEL > 1 ) {
		print "Syncing $DESTINATION_DIRECTORY (destination) with\n";
		print "        $SOURCE_DIRECTORY (source)...\n";
	}
	elsif ( $LOG_LEVEL > 0 ) {
		print "Starting sync of $SOURCE_DIRECTORY\n to $DESTINATION_DIRECTORY\n";
	}
	sync_collection_directories();
	$LIST_TYPE = 'file';
	foreach (@EXTENSION_LIST) {
		eval $PRINT_LINE_LARGE;
		$EXTENSION = $_;
		@FOUND_LIST = ();
		File::Find::find( \&wanted, $SOURCE_DIRECTORY);
		# print Dumper \@FOUND_LIST;
		if ( $LOG_LEVEL > 1 ) {
			print "PROCESSING DATA TYPE: $EXTENSION\n";
		}
		elsif ( $LOG_LEVEL > 0 ) {
			print "\n" . dotify("Processing $EXTENSION data type");
		}
		if (@FOUND_LIST){
			sync_collection_files();
		}
		else {
			print_not_found('extension');
		}
	}
}
# Recreate the directory hierarchy.
sub sync_collection_directories {
	my ($b_created,$dest_dir,$dir,$result);
	$LIST_TYPE = 'dir';
	File::Find::find( \&wanted, "$SOURCE_DIRECTORY");
	eval $PRINT_LINE_LARGE;
	# print Dumper \@FOUND_LIST;
	if ( $LOG_LEVEL > 1 ) {
		print "Checking if $SELF_NAME needs to create destination directories...\n";
	}
	elsif ( $LOG_LEVEL > 0 ) {
		print dotify("Updating destination directories");
	}
	foreach $dir (@FOUND_LIST) {
		#print "\nd1:$dir\n";
		$result = 'UNSET';
		next if $dir eq $SOURCE_DIRECTORY || $dir eq $DESTINATION_DIRECTORY;
		$dir =~ s/^\.\///; # delete any dot file directories
		$dir =~ s|^$SOURCE_DIRECTORY/||; # strip out source path
		next if !$dir;
		$dest_dir = "$DESTINATION_DIRECTORY/$dir";
		#print "d2:$dir\n";
		# check to see if the destination dir already exists
		if ( ! stat("$dest_dir") ) {
			# stat failed so create the directory
			eval $PRINT_LINE_SMALL;
			if ( $LOG_LEVEL > 1 ) {
				print "CREATING NEW DIRECTORY:\n  $dest_dir\n";
			}
			elsif ( $LOG_LEVEL > 0 ) {
				print "\nCreating new directory: $dest_dir";
			}
			$dest_dir =~ s/\`/\'/g; # get rid of weird characters
			$result = mkdir("$dest_dir") if !$B_TEST;
			$b_created = 1;
			$B_DEST_CHANGED = 1;
			print "Create Directory result: $result\n" if $LOG_LEVEL > 1;
		}
	}
	print_not_found('dirs') if ! $b_created;
}
sub sync_collection_files {
	my ($b_created,$cmd,$result);
	my ($action,$dest_file,$file,$src_file) = ('','','','');
	my ($dest_info,$dest_mod_time,$src_info, $src_mod_time) = ('',0,'',0);
	foreach $file (@FOUND_LIST) {
		# $file =~ s/\n$//;
		$file =~ s/^\.\///; # get rid of hidden files
		next if ! $file;
		$dest_file = $src_file = $file;
		#print "\nIF: $src_file\n";
		$dest_file =~ s|^$SOURCE_DIRECTORY|$DESTINATION_DIRECTORY|;
		$dest_file =~ s/\`/\'/g;
		# $dest_file =~ s/\$/\\\$/g;
		# get rid of escape sequences in case someone used them: \40
		# $src_file =~ s/\0//g; 
		$dest_file =~ s/\0//g;
		# Figure out what the destination file would be...
		if ( lc($EXTENSION) eq $INPUT_TYPE ){
			$dest_file =~ s/\.$INPUT_TYPE$/\.$OUTPUT_TYPE/i;
		}
		#print "OF: $dest_file\n";
		($result,$dest_mod_time,$src_mod_time ) = ('UNSET',0,0);
		# Now stat the destinationFile, and see if it's date is more recent
		# than that of the original file. If so, we re-encode.
		# We also re-encode if the user supplied --force
		$src_info = stat("$src_file") or 
		  error_handler('stat-infile', "No $src_file: $!");
		$src_mod_time = $src_info->mtime if $src_info;
		$dest_info = stat("$dest_file");
		if ( $dest_info ) {
			$dest_mod_time = $dest_info->mtime;
			# print "DEST_MOD: $dest_mod_time :: SRC_MOD: $src_mod_time :: FORCE: $force\n"; 
# 		} else {
# 			print "NOT EXISTS: $destinationFile \n"; 
# 			print "P1: $file ==> \n  $destinationFile\n"; 
		}
		# If the destination file does not exist, or the user specified force,
		# or the srcfile is more recent then the dest file, we encode/copy.
		# print "src-mt: $src_mod_time dest-mt:$dest_mod_time\n";
		if ( !$dest_info || $B_FORCE || ( $src_mod_time > $dest_mod_time) ) {
			eval $PRINT_LINE_SMALL;
			if ( lc($EXTENSION) eq $INPUT_TYPE ){
				if ( $LOG_LEVEL > 1 ) {
					print "ENCODE: $src_file ==> \n";
					print "        $dest_file\n"; 
				}
				elsif ( $LOG_LEVEL > 0 ) {
					# add line break only when file exists
					print dotify("\nEncoding $src_file to $OUTPUT_TYPE");
				}
				if ( $OUTPUT_TYPE eq 'ogg' ){
					$action = 'To ogg';
					if (!$B_TEST){
						qx($COMMAND_OGG $SILENT_ARG_FO -q $QUALITY -o "$dest_file" "$src_file");
						$result = $?;
					}
				}
				elsif ( $OUTPUT_TYPE eq 'mp3' ){
					$action = 'To mp3';
					# Modified: Odd @2011-03-23 01:51:26 - Include ID3 copying
					$cmd = flac2mp3_cmd("$src_file","$dest_file");
					if (!$B_TEST){
						qx($cmd);
						$result = $?;
					}
				}
			} 
			else {
				$action = 'Copy';
				if ( $LOG_LEVEL > 1 ) {
					print "COPY: $file ==> \n";
					print "      $dest_file\n"; 
				}
				elsif ( $LOG_LEVEL > 0 ) {
					print "\nCopying $file...";
				}
				if (!$B_TEST){
					$result = copy($src_file, $dest_file) or 
					  error_handler('cp-file', "cp failure: $src_file =>\n$dest_file\nCode: $!");
				}
			}
			$b_created = 1;
			$B_DEST_CHANGED = 1;
			print "$action result: $result\n" if $LOG_LEVEL > 1;
		} 
	}
	print_not_found('files') if ! $b_created;
}

# Added: Odd @2011-03-23 01:52:31
sub flac2mp3_cmd {
	my ($i_file, $o_file) = @_;
	my %tags = get_flac_tags("$i_file", ("ARTIST", "ALBUM", "TITLE", 
	"GENRE", "DATE", "TRACKNUMBER"));
	# print Dumper \%tags;
	my $lame_params = "--ta \"$tags{'ARTIST'}\" --tl \"$tags{'ALBUM'}\"";
	$lame_params .= " --tt \"$tags{'TITLE'}\" --tg \"$tags{'GENRE'}\"";
	$lame_params .= " --ty \"$tags{'DATE'}\" --tn \"$tags{'TRACKNUMBER'}\" - \"$o_file\"";
	my $cmd = "$COMMAND_FLAC $SILENT_ARG_FO -d -c \"$i_file\" | ";
	$cmd .= "$COMMAND_LAME $SILENT_ARG_LAME -h -V $QUALITY $lame_params";
	return $cmd;
}

# Added: Odd @2011-03-23 01:52:17
# For this function to work reliably, it should be passed tag queries in the order of:
# artist, album, title, genre, date, tracknumber
sub get_flac_tags {
	my ($i_file, @tags) = @_;
	my (%tag_list,@working);
	my $cmd = "$COMMAND_METAFLAC \"$i_file\" --show-tag=\"$tags[0]\"";
	$cmd .= " --show-tag=\"$tags[1]\" --show-tag=\"$tags[2]\" --show-tag=\"$tags[3]\"";
	$cmd .= " --show-tag=\"$tags[4]\" --show-tag=\"$tags[5]\"";
	my @orig_tags = qx($cmd);
	chomp @orig_tags;
	foreach (@orig_tags) {
		@working = split /=/,$_;
		$tag_list{$working[0]} = $working[1];
	}
	# create missing hash keys
	foreach (@tags){
		$tag_list{$_} = '' if ! defined $tag_list{$_};
	}
	# print Dumper \%tag_list;
	return %tag_list;
}

### UTILITIES ###
# NOTE: File::Find will not follow symbolic links
sub wanted {
	return if -l; # skip symbolic links
	return if $File::Find::name =~ /^$DESTINATION_DIRECTORY/;
	if ($LIST_TYPE eq 'dir'){
		return if ! -d; # only directories
	}
	else {
		return if -d; # skip directories
		return if $File::Find::name !~ /\.$EXTENSION$/i;
	}
	# print $File::Find::name . "\n";
	push (@FOUND_LIST, $File::Find::name);
	return;
}
sub dotify {
	my $string = $_[0];
	while (length($string) < 53){
		$string .= '.';
	}
	$string .= ' ';
	return $string;
}
sub print_not_found {
	my ($message,$none);
	if ( $_[0] eq 'files'){
		$message = "No files to update of type: $EXTENSION\n";
		$none = "None to update";
	}
	elsif ( $_[0] eq 'extension'){
		$message = "No files found of type: $EXTENSION\n";
		$none = "None found";
	}
	else {
		$message = "No new directories required. Continuing...\n";
		$none = "None required";
	}
	if ( $LOG_LEVEL > 1 ) {
		print $message;
	}
	elsif ( $LOG_LEVEL > 0 ) {
		print $none;
	}
}
sub print_completion_message {
	eval $PRINT_LINE_HEAVY;
	if ( $B_DEST_CHANGED ) {
		if ( $LOG_LEVEL > 1 ) {
			print "All done updating. Enjoy your music!\n";
		}
		elsif ( $LOG_LEVEL > 0 ) {
			print "\nUpdating completed. Enjoy your music!\n";
		}
	}
	elsif ( $LOG_LEVEL > 0 ) {
		print "\nThere was nothing to update today in your collection.\n";
	}
	exit 0;
}

### PRINT HELP/VERSION ###
sub print_help {
	# so it shows the user config data if present
	set_config_data(); 
	# but this should override the config data so follows
	set_basic_data();
	my $output = "$SELF_NAME v: $SELF_VERSION :: Supported Options:\n";
	$output .= "Examples: $SELF_NAME -q 8 --destination /music/main/ogg\n";
	$output .= "$SELF_NAME --input wav --output ogg\n";
	$output .= "$SELF_NAME --copy doc,docx,bmp\n";
	$output .= $LINE_SMALL;
	$output .= "--copy -c         List of alternate data types to copy to Output type\n";
	$output .= "                  directories. Must be comma separated, no spaces, \n";
	$output .= "                  see sample above. Your current copy types are:\n";
	$output .= "                  @EXTENSION_LIST\n";
	$output .= "--debug           Full screen output, including full verbosity of \n";
	$output .= "                  flac/oggenc/lameconversion process for MP3 or OGG output.\n";
	$output .= "--default         Single line per operation screen output, default value for \n";
	$output .= "                  $SELF_NAME.\n";
	$output .= "--destination -d  Path to the directory where you want the processed\n";
	$output .= "                  (eg, ogg) files to go.\n";
	$output .= "                  Your current default is: $DESTINATION_DIRECTORY\n";
	$output .= "--force -f        Overwrite the ogg/jpg/txt files, even if they already exist.\n";
	$output .= "--help -h         This help menu.\n";
	$output .= "--input -i        Input type: supported - flac, wav, raw. mp3 only supports\n";
	$output .= "                  flac input. Your current default is: $INPUT_TYPE\n";
	$output .= "--output -o       Output type: supported - ogg, mp3\n";
	$output .= "                  Your current default is: $OUTPUT_TYPE\n";
	$output .= "--quality n -q n  For ogg: n can be 1-10. 10 is the largest file/\n";
	$output .= "                  highest quality.\n";                
	$output .= "                  For mp3: n can be 0-9 (variable bit rate), 0 is largest file/\n";
	$output .= "                  highest quality. \n";
	$output .= "                  Your current default is: $QUALITY\n";
	$output .= "--quiet --silent  Turns off all screen output, except for error messages.\n";
	$output .= "--source -s       Path to the top-most directory containing your source files\n";
	$output .= "                  (eg, flac). Your current default is: $SOURCE_DIRECTORY\n";
	$output .= "--verbose         Without full verbosity of debug, no flac/oggenc/lame for MP3\n";
	$output .= "                  or OGG conversion process screen output, but more verbose than\n";
	$output .= "                  default.\n";
	$output .= "--test            Test copy and sync without actually doing the copy/sync. Also\n";
	$output .= "                  activates --debug for more complete path outputs.\n";
	$output .= "--version -v      Show $SELF_NAME version.\n\n";
	$output .= $LINE_SMALL;
	$output .= "User Configs (checked in this order):\n";
	$output .= "/etc/$SELF_NAME.conf\n";
	$output .= "\$XDG_CONFIG_HOME/$SELF_NAME.conf\n";
	$output .= "\$HOME/.config/$SELF_NAME.conf\n";
	$output .= "\$HOME/.$SELF_NAME.conf\n";
	$output .= "Requires this syntax (any user modifiable variable can be used)\n";
	$output .= "DIR_PREFIX_SOURCE=/home/me/music/flac\n";
	$output .= "Do not use \$, \", or \' in config options or values.\n";
	$output .= "\n";
	print $output;
	exit 0;
}
sub print_version {
	print "$SELF_NAME version: $SELF_VERSION ($SELF_DATE)\n";
	exit 0;
}

### SET PROGRAM DATA ###
sub set_config_data {
	my (@config_files,$file,$user_config);
	# set list of supported config files
	@config_files = ("/etc/$SELF_NAME.conf");
	if ($ENV{'XDG_CONFIG_HOME'} && -r "$ENV{XDG_CONFIG_HOME}/$SELF_NAME.conf"){
		$user_config = "$ENV{'XDG_CONFIG_HOME'}/$SELF_NAME.conf";
	}
	elsif ( -r "$ENV{HOME}/.config/$SELF_NAME.conf"){
		$user_config = "$ENV{HOME}/.config/$SELF_NAME.conf";
	}
	elsif (-r "$ENV{HOME}/.$SELF_NAME.conf"){
		$user_config = "$ENV{HOME}/.$SELF_NAME.conf";
	}
	@config_files = (@config_files,$user_config) if $user_config;
	foreach $file (@config_files) {
		next unless -r $file && open (my $fh,'<',$file);
		while (<$fh>) {
			chomp;                  # no newline
			s/#.*//;                # no comments
			s/^\s+//;               # no leading white
			s/\s+$//;               # no trailing white
			s/('|"|\$|\s)//g;       # get rid of all non valid characters
			next unless length;     # anything left?
			my ($var, $value) = split(/\s*=\s*/, $_, 2);
			if (defined $var && defined $value){
				assign_config_value($var, $value);
			}
		}
	}
}
sub assign_config_value {
	my ($var,$value) = @_;
	if ($var eq 'COMMAND_OGG'){$COMMAND_OGG = $value;}
	elsif ($var eq 'COMMAND_FLAC'){$COMMAND_FLAC = $value;}
	elsif ($var eq 'COMMAND_LAME'){$COMMAND_LAME = $value;}
	elsif ($var eq 'COMMAND_METAFLAC'){$COMMAND_METAFLAC = $value;}
	elsif ($var eq 'SOURCE_DIRECTORY' || $var eq 'DIR_PREFIX_SOURCE'){
		$SOURCE_DIRECTORY = $value;}
	elsif ($var eq 'DESTINATION_DIRECTORY' || $var eq 'DIR_PREFIX_DEST'){
		$DESTINATION_DIRECTORY = $value;}
	elsif ($var eq 'INPUT_TYPE'){$INPUT_TYPE = $value;}
	elsif ($var eq 'LOG_LEVEL'){$LOG_LEVEL = $value;}
	elsif ($var eq 'OUTPUT_TYPE'){$OUTPUT_TYPE = $value;}
	elsif ($var eq 'QUALITY'){$QUALITY = $value;}
	elsif ($var eq 'COPY_TYPES' || $var eq 'USER_TYPES'){
		$COPY_TYPES = $value;}
}
# get defaults from user config files if present
set_config_data();
# Get Options and set values, this overrides defaults 
# from top globals and config files
GetOptions (
	# accepts null value so users can not copy anything
	"c|copy:s" => sub { 
		my ($opt,$arg) = @_;
		$COPY_TYPES = $arg;},
	"debug" => sub { 
		$LOG_LEVEL = 3 },
	"default" => sub { 
		$LOG_LEVEL = 1 },
	"d|destination:s" => sub { 
		my ($opt,$arg) = @_;
		error_handler('missing-arg',"$opt requires path.") if ! $arg;
		$DESTINATION_DIRECTORY = $arg },
	"f|force" => sub { 
		$B_FORCE = 1 },
	"h|help|?" => sub { 
		print_help();},
	"i|input:s" => sub { 
		my ($opt,$arg) = @_;
		error_handler('missing-arg',"$opt requires type.") if ! $arg;
		$INPUT_TYPE = $arg },
	"o|output:s" => sub {
		my ($opt,$arg) = @_;
		error_handler('missing-arg',"$opt requires type.") if ! $arg;
		$OUTPUT_TYPE = $arg;},
	"q|quality:s" => sub { 
		my ($opt,$arg) = @_;
		error_handler('missing-arg',"$opt requires number.") if ! defined $arg || $arg !~ /^[09-]+$/;
		$QUALITY = $arg }, # validate later
	"s|source:s" => sub { 
		my ($opt,$arg) = @_;
		error_handler('missing-arg',"$opt requires path.") if ! $arg;
		$SOURCE_DIRECTORY = $arg;},
	"quiet|silent" => sub { 
		$B_QUIET = 1; 
		$LOG_LEVEL = 0 },
	"test" => sub { 
		$LOG_LEVEL = 3;
		$B_TEST = 1 },
	"verbose" => sub { 
		$LOG_LEVEL = 2 },
	"v|version" => sub { 
		print_version(); },
	'<>' => sub {
		my ($opt) = @_;
		error_handler('unsupported-option', "Unsupported option: $opt"); }
);

### EXECUTE / PROCESS MUSIC ###
set_display_output_data();
validate_start_text();
validate_log_level();
validate_src_dest_directories();
validate_in_out_types();
validate_quality();
validate_application_paths();
sync_music_collections();
print_completion_message();

###**EOF**###
